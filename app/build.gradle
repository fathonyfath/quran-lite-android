import java.text.SimpleDateFormat

buildscript {
    project.ext.version = [
            'major': 1,
            'minor': 3,
            'patch': 0,
    ]

    project.ext.version['name'] = "${project.ext.version.major}.${project.ext.version.minor}.${project.ext.version.patch}"
    project.ext.version['code'] = project.ext.version.major * 10000000 + project.ext.version.minor * 10000 + project.ext.version.patch
}

plugins {
    id 'com.android.application'
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

android {
    namespace 'id.thony.android.quranlite'
    compileSdk 34
    defaultConfig {
        applicationId "id.thony.android.quranlite"
        minSdk 18
        targetSdk 34
        versionCode project.ext.version.code
        versionName "${project.ext.version.name}-${gitSha()} (${buildTime()})"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
